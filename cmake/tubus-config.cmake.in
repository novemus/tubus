@PACKAGE_INIT@

macro(import_targets _type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/tubus-${_type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "tubus ${_type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/tubus-${_type}-targets.cmake")
endmacro()

if(NOT TARGET tubus::libtubus)
    set(_type "")

    if(DEFINED TUBUS_USE_SHARED_LIB)
        if(TUBUS_USE_SHARED_LIB)
            set(_type "shared")
        else()
            set(_type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/tubus-shared-targets.cmake")
        set(_type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/tubus-static-targets.cmake")
        set(_type "static")
    else()
        set(_type "shared")
    endif()

    import_targets(${_type})
    check_required_components(tubus)

    include(CMakeFindDependencyMacro)
    find_dependency(Boost COMPONENTS system)

    get_target_property(tubus_INCLUDE_DIRS tubus::libtubus INTERFACE_INCLUDE_DIRECTORIES)
    set(tubus_LIBRARY tubus::libtubus)

    set(tubus_FOUND ON)
    unset(_type)
endif()
